import com.github.triplet.gradle.androidpublisher.ResolutionStrategy
import groovy.json.JsonSlurper

apply plugin: 'com.android.application'
apply from: rootDir.absolutePath + "/gradle/config-android.gradle"
apply from: rootDir.absolutePath + "/gradle/config-util.gradle"
apply from: rootDir.absolutePath + "/gradle/config-junit5.gradle"
apply from: rootDir.absolutePath + "/gradle/config-hilt.gradle"
apply from: rootDir.absolutePath + "/gradle/config-compose.gradle"

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.github.triplet.play'

apply from: rootDir.absolutePath + "/gradle/script-releasenotes.gradle"

android {
    compileSdkVersion project.ext.compileSdkVersion

    defaultConfig {
        applicationId "tmg.hourglass"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode project.ext.versionCode // generatedVersionCode
        versionName project.ext.versionName // generatedVersionName

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "hour-glass.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'version'
    productFlavors {
        sand {
            dimension 'version'
            applicationIdSuffix '.sand'
            versionNameSuffix '-sand'

            buildConfigField "int", "ENVIRONMENT", '1'
        }

        live {
            dimension 'version'

            buildConfigField "int", "ENVIRONMENT", '0'
        }
    }

    packagingOptions {
        exclude "META-INF/LICENSE*"
        exclude "META-INF/library_release.kotlin_module"
        exclude "lib/arm64-v8a/librealm-jni.so"
    }
}

play {
    track = 'internal'
    serviceAccountCredentials = file(System.getenv("PRIVATE_KEY") ?: "hour-glass.json")
    defaultToAppBundles = true
    resolutionStrategy = ResolutionStrategy.IGNORE
}

// Adds generate tasks to assemble
tasks.whenTaskAdded { task ->
    if (task.name.contains("assemble")) {
        task.dependsOn generateReleaseNotes
    }
}

dependencies {

    // Modules
    implementation project(path: ":realm")
    implementation project(path: ":domain")
    implementation project(path: ":presentation")

    implementation libs.androidx.splashscreen

    // Firebase
    implementation platform(libs.firebase.bom)
    implementation libs.firebase.analytics
    implementation libs.firebase.crashlytics
    implementation libs.firebase.perf

    // TMG
    implementation libs.tmg.aboutthisapp
    implementation libs.tmg.utils
    implementation libs.tmg.labelledprogressbar

    // BugShaker
    implementation libs.falcon
    implementation libs.shakey

    // Accompanist
    implementation libs.accompanist.systemuicontroller

    // ColorSheet
    implementation libs.colorsheet
}
